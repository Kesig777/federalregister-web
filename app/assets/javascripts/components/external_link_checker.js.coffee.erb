class @FR2.ExternalLinkChecker
  @WHITELISTED_DOMAINS = <%= Settings.external_modal_domain_whitelist %>

  constructor: (@event)->
    @el = $(@event.target)
    @url = @el.attr('href')

    if @isExternalUrl() && !@isOwnDomain() && @showModal() && !@isWhitelistedDomain()
      @event.preventDefault()
      @displayModal()

  displayModal: ->
    modalBody = Handlebars.compile($('#external-link-warning-modal-template').html())({
      url: @url
    })

    FR2.Modal.displayModal(
      'You Are Now Leaving the Federal Register',
      modalBody,
      {modalClass: 'fr-modal external-link-warning-modal'}
    )

    @addModalEvents()

  addModalEvents: ->
    $('#fr_modal.external-link-warning-modal form').on 'submit', (e)=>
      e.preventDefault()

      if $('#fr_modal.external-link-warning-modal input[name=accept]').prop('checked')
        amplify.store('showExternalModal', false, expires: 2592000000)

      if @el.attr('target') == "_blank"
        FR2.Modal.closeModal('#fr_modal')
        window.open(@url)
      else
        window.location.href = @url

  showModal: ->
    amplify.store('showExternalModal') != false

  isOwnDomain: ->
    RegExp("^#{window.location.origin}").test(@url)

  isExternalUrl: ->
    RegExp('^(https?:)//').test(@url)

  isWhitelistedDomain: ->
    _.any @constructor.WHITELISTED_DOMAINS, (domain)=>
      RegExp("^https?://#{domain}").test(@url)
